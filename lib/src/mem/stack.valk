
@ignore_access_types

use mem
use os
use io
use core

global stack : @ignu(Stack)
global run_gc : bool

global mem_new : uint
global mem_marked : uint
global mem_transfered : uint
global mem_usage : uint
global mem_gc_loop : bool

value mem_marked_default (8192)

value state_new (0)
value state_used (1)
value state_transfer (2)
value state_solid (4)
value state_const (8)
value state_shared (10)
value stack_size (10000)

value vt_hook_transfer (0)
value vt_hook_mark (1)
value vt_hook_mark_shared (2)
value vt_hook_share (3)
value vt_hook_free (4)

value byte_state (0)
value byte_age (1)
value byte_shared_age (2)
value byte_pool_index (3)
value byte_block_index (4)
value byte_gc_prop_count (5)
value byte_has_vtable (6)
value byte_unused (7)

value byte_state_rel (-8)
value byte_age_rel (-7)
value byte_shared_age_rel (-6)
value byte_pool_index_rel (-5)
value byte_block_index_rel (-4)
value byte_gc_prop_count_rel (-3)
value byte_has_vtable_rel (-2)
value byte_unused_rel (-1)

shared stack_count: uint

global stack_pos: @ignu(StackPos)
global main_stack_pos: @ignu(StackPos)

cstruct StackPos {
    base: ptr
    adr: ptr
    size: uint
    fn free() {
        free(this.base)
        free(this)
    }
}
fn create_stack() StackPos {
    let base = alloc(stack_size)
    return StackPos { 
        base: base
        adr: base
        size: stack_size
    }
}

cstruct Stack {
    // id: uint

    transfer : ObjectChain
    shared : GcRing
    lock : GcLock
    bump: GcBumper (GcBumper.new())

    globals: ptr (null @as ptr)
    globalc: uint (0)

    age: u8 (33)
    shared_age: u8 (0)

    run_gc_ref: ptr
    bumper_overflow: bool (false)

    force_full: bool (false)
    stopped: bool (false)

    static fn init() {

        gc_alloc_init()
        // let id = atomic(stack_count + 1)

        let pos = create_stack()
        stack_pos = pos
        main_stack_pos = pos

        let s = Stack {
            // id: id
            transfer: ObjectChain.new()
            shared: GcRing.new()
            lock: GcLock.new()
            run_gc_ref: @ptr_of(run_gc)
        }

        stack = s
        run_gc = false
        mem_marked = mem_marked_default

        init_pools()

        s.scan_globals()

        let gcm = gc_manager()
        gcm.add_stack(s)

        core:g_coros = Array[?core:Coro].new()
        core:g_coro_indexes = Pool[uint].new()
    }

    #parse_last
    fn scan_globals() {
        let count : uint = 0
        #loop_globals as G, T
        #if @type_is_gc(T) && !@global_is_shared(G)
        count++
        #end
        #endloop

        let globals = alloc(sizeof(ptr) * count)
        let globalc = count
        count = 0
    
        #loop_globals as G2, T2
        #if @type_is_gc(T2) && !@global_is_shared(G2)
        @ptrv(globals, ptr, count++) = @ptr_of(G2)
        #end
        #endloop
    }

    ///////////////////////
    // Runtime functions
    ///////////////////////
    static fn link(on: ptr, to: ptr) {
        let to_state = @ptrv(to, u8, -8)
        let on_state = @ptrv(on, u8, -8)
        if on_state == state_shared {
            if to_state != state_shared {
                gc_share(to)
            } else {
                gc_mark_shared(to, shared_age)
            }
            return
        }
        if to_state < state_transfer {
            // Transfer
            stack.transfer.add_item(to)
            @ptrv(to, u8, -8) = state_transfer
        }
    }

    ///////////////////////
    // Gc
    ///////////////////////
    fn gc() {
        this.lock.lock()

        mem_new = 0
        mem_usage = 0
        run_gc = false
        mem_gc_loop = false

        this.mark_difference_down()
        this.handle_transfer_list()
        pools_swap_transfers()
        this.bump.wipe()

        let transfer_percent = mem_transfered * 100 / mem_marked

        // Full clear
        if transfer_percent > 20 || this.force_full {

            this.force_full = false

            let before = mem_marked
            let total_before = before + mem_transfered
            // Reset counters
            mem_marked = mem_marked_default
            mem_transfered = 0
            // Mark
            this.update_age()
            this.mark_stack_age()
            // Clean
            let freed = total_before - mem_marked
            if freed > mem_marked / 10 || this.force_full {
                // Clean
                pools_clean_unmarked(this.age)
            }

            // Mark shared
            if shared_age != this.shared_age {
                let age = shared_age
                this.mark_stack_shared_age(age)
                this.shared_age = age
            }
        }
        pools_reset()

        this.lock.unlock()
    }

    #parse_last
    fn mark_difference_down() {
        //
        let transfer = this.transfer

        // Globals
        #loop_globals as G, T
        #if @type_is_gc(T) && !@global_is_shared(G)
        {
            let g = G @as ptr
            if g != null {
                let state = @ptrv(g, u8, -8)
                if state < state_transfer {
                    // Transfer new items
                    transfer.add_item(g)
                    @ptrv(g, u8, -8) = state_transfer
                } 
            }
        }
        #end
        #endloop

        // Stack
        let pos = main_stack_pos
        let stack_base = pos.base
        let stack_adr = pos.adr

        // Mark new items as transfer
        let adr = stack_adr
        while adr > stack_base {
            let item_adr = adr - sizeof(ptr)
            adr = item_adr
            let item = @ptrv(item_adr, ptr, 0)
            if item != null {
                //
                let state = @ptrv(item, u8, -8)
                if state < state_transfer {
                    // Transfer new items
                    transfer.add_item(item)
                    @ptrv(item, u8, -8) = state_transfer
                } 
            }
        }

        // Coroutines
        core:Coro.transfer_check()
    }

    fn handle_transfer_list() {
        let chain = this.transfer
        let last_list = chain.olist_last
        let last_index = chain.index
        let first_list = chain.olist_first
        let next_list = first_list

        // Mark solid
        while true {
            let list = next_list
            let index : uint = 0
            let is_last = list == last_list
            let until_ : uint = chain_size
            if is_last {
                until_ = last_index
            }
            let until = until_
            while index < until {
                let item = @ptrv(list, ptr, index)
                index++
                // Item logic
                if @ptrv(item, u8, byte_state_rel) != state_transfer {
                    continue
                }

                gc_transfer(item)
            }
            if is_last {
                break
            }
            next_list = @ptrv(list, ptr, chain_size)
            if next_list == null {
                break
            }
        }

        chain.wipe()
    }

    fn update_age() {
        let age : u8 = 64
        if this.age == 64 {
            age = 66
        }
        this.age = age
    }

    #parse_last
    fn mark_stack_age() {
        let age = this.age
        // Globals
        #loop_globals as G, T
        #if @type_is_gc(T) && !@global_is_shared(G)
        {
            let item = G @as ptr
            if item != null {
                if @ptrv(item, u8, byte_state_rel) < state_const {
                    if @ptrv(item, u8, byte_age_rel) != age {
                        gc_mark(item, age)
                    }
                }
            }
        }
        #end
        #endloop
        // Stack
        let pos = main_stack_pos
        let item_adr = pos.base
        let stack_adr = pos.adr
        while item_adr < stack_adr {
            let item = @ptrv(item_adr, ptr)
            item_adr += sizeof(ptr)
            if item != null {
                if @ptrv(item, u8, byte_state_rel) < state_const {
                    if @ptrv(item, u8, byte_age_rel) != age {
                        gc_mark(item, age)
                    }
                }
            }
        }
    }
    fn mark_stack_shared_age(age: u8) {
        // Globals
        let globals = this.globals
        let globalc = this.globalc
        while globalc-- > 0 {
            let ref = @ptrv(globals, ptr, globalc)
            let item = @ptrv(ref, ptr)
            if item != null {
                if @ptrv(item, u8, byte_shared_age_rel) != age {
                    gc_mark_shared(item, age)
                }
            }
        }
        // Stack
        let pos = main_stack_pos
        let item_adr = pos.base
        let stack_adr = pos.adr
        while item_adr < stack_adr {
            let item = @ptrv(item_adr, ptr)
            if item != null {
                if @ptrv(item, u8, byte_shared_age_rel) != age {
                    gc_mark_shared(item, age)
                }
            }
            item_adr = item_adr + sizeof(ptr)
        }
    }

    fn add_shared(item: ptr) {
        let ring = this.shared
        ring.lock.lock()
        ring.add(item)
        ring.lock.unlock()
    }
    fn clean_shared() {
        let ring = this.shared
        ring.lock.lock()
        let tail = ring.tail
        let head = ring.head
        let size = ring.size
        let data = ring.data
        let age = shared_age
        while tail != head {
            let item = @ptrv(data, ptr, tail)
            let item_age = @ptrv(item, u8, byte_shared_age_rel)
            if item_age != age {
                // Free item
                @ptrv(item, u8, byte_state_rel) = state_solid
                ring.remove(tail)
            }

            tail++
            if tail == size {
                tail = 0
            }
        }
        ring.lock.unlock()
    }

    fn shutdown() {
        //
        this.force_full = true
        let pos = main_stack_pos
        pos.adr = pos.base
        this.gc()
        //
        this.transfer.delete()

        //GcPool.delete_all()
        // TODO: delete pools

        // Stack is deleted by the Gc
        this.stopped = true
    }


    fn print() {
        print("---------\n")
        let pos = main_stack_pos
        let item_adr = pos.base
        let stack_adr = pos.adr
        while item_adr < stack_adr {
            let item = @ptrv(item_adr, ptr)
            item.print()
            print("\n")
            item_adr = item_adr + sizeof(ptr)
        }
        print("---------\n")
    }
}

