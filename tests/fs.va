
use valk:fs
use valk:io
use valk:os

test "fs:dir_of" {
    assert(fs:dir_of("/etc/nginx/nginx.conf") == "/etc/nginx")
    assert(fs:dir_of("/etc/nginx/") == "/etc")
    assert(fs:dir_of("/etc/nginx") == "/etc")
    assert(fs:dir_of("/etc") == "/")
    assert(fs:dir_of("/") == "/")
    assert(fs:dir_of("etc") == "etc")
    assert(fs:dir_of("") == "")
    // TODO: windows paths
}

test "fs:basename" {
    assert(fs:basename("/etc/nginx/nginx.conf") == "nginx.conf")
    assert(fs:basename("/etc/nginx/") == "nginx")
    assert(fs:basename("/") == "")
    assert(fs:basename("") == "")
    // TODO: windows paths
}

test "fs: create / delete directories & files" {
    // Create dir
    // let dir = fs:exe_dir() + "/valk-mkdir-test/"
    let dir = fs:exe_dir() + "/valk-mkdir-test/"
    fs:mkdir(dir, 511)
    assert(fs:exists(dir))

    // Open & create file
    let file = dir + "test.txt"
    let fd = fs:open_extend(file, true, true, true) ? 10000
    assert(fd != 10000)
    assert(fs:exists(file))

    // Files in dir
    let files = fs:files_in(dir)
    assert(files.length == 1)
    let fn = files.get(0) ? ""
    assert(fs:basename(fn) == "test.txt")

    // Write to FD
    if fd != 10000 {
        io:write(fd, "hello") ! {
            assert(false)
        }
        io:write(fd, "-world") ! {
            assert(false)
        }
        let content = fs:read(file) ? ""
        assert(content == "hello-world")

        io:close(fd)
    }

    // Delete file
    if fs:exists(file) {
        fs:remove(file)
    }
    assert(fs:exists(file) == false)

    // Delete dir
    fs:rmdir(dir)
    assert(fs:exists(dir) == false)
}
