
use mem
use io

class Array[T] {
    ~ size: uint
    ~ length: uint (0)
    ~ data: ptr

    static fn new(start_size: uint (2)) CLASS {
        if start_size < 2 {
            start_size = 2
        }
        return CLASS {
            size: start_size
            data: mem:alloc(start_size * sizeof(T))
        };
    }

    fn push(item: T) uint {
        let size = this.size
        let length = this.length
        let data = this.data

        if length == size {
            data = this.increase_size()
        }

        @ptrv(data, T, length) = @gc_link(this, item)
        this.length = length + 1

        return length
    }

    fn prepend(item: T) void {

        let size = this.size
        let length = this.length
        let data = this.data

        if length == size {
            data = this.increase_size()
        }

        let to_pos = length
        while to_pos > 0 {
            @ptrv(data, T, to_pos) = @ptrv(data, T, to_pos - 1)
            to_pos--
        }

        @ptrv(data, T, 0) = @gc_link(this, item)
        this.length = length + 1
    }

    fn increase_size() ptr {
        let data = this.data
        let size = this.size
        let new_size = size * 2
        let new_data : ptr = null @as ptr

        new_data = mem:alloc(new_size * sizeof(T));
        mem:copy(data, new_data, size * sizeof(T));
        mem:free(data)

        this.data = new_data
        this.size = new_size
        return new_data
    }

    fn get(index: uint) T !not_found {
        if index >= this.length {
            throw not_found;
        }
        return @ptrv(this.data, T, index);
    }

    fn set(index: uint, value: T) void !out_of_range {
        if index >= this.length {
            throw out_of_range;
        }
        let data = this.data;
        @ptrv(data, T, index) = @gc_link(this, value)
    }

    fn contains(value: T) bool {
        let index = this.index_of(value) ! {
            return false
        }
        return true
    }

    fn remove_index(index: uint) {
        let len = this.length;
        if index >= len { return }
        let data = this.data;
        let after_count = len - (index + 1);

        if after_count > 0 {
            mem:copy(data + (index + 1) * sizeof(T), data + index * sizeof(T), after_count * sizeof(T));
        }

        this.length--;
    }
    fn remove_value(value: T) {
        let index = this.index_of(value) ! {
            return
        }
        this.remove_index(index);
    }

    fn pop() T !empty {
        if this.length > 0 {
            return @ptrv(this.data, T, this.length - 1);
        }
        throw empty
    }

    fn swap(index_a: uint, index_b: uint) {
        let len = this.length
        if index_a >= len {
            return
        }
        if index_b >= len {
            return
        }
        let data = this.data
        let a = @ptrv(data, T, index_a)
        let b = @ptrv(data, T, index_b)
        @ptrv(data, T, index_a) = b
        @ptrv(data, T, index_b) = a
    }

    fn index_of(item: T) uint !not_found {
        let index : uint = 0
        let adr = this.data
        let len = this.length
        while index < len {
            let x = @ptrv(adr, T, index)
            if x == item {
                return index
            }
            index++
        }
        throw not_found
    }

    fn clear() {
        this.length = 0
    }

    // fn sort() {
    //     let len_ = this.length
    //     if len_ < 2 { return }

    //     let v = true
    //     let i : uint = 0
    //     let len = len_ - 1
    //     while i < len && v {
    //         v = false
    //         let j : uint = 0
    //         let until = len - i
    //         while j < until {
    //             let item = this.get(j) ! { break }
    //             let next = this.get(j + 1) ! { break }
    //             if item > next {
    //                 this.swap(j, j + 1)
    //                 v = true
    //             }
    //             j++
    //         }
    //         i++
    //     }
    // }

    // fn reverse() {
    //     let len_ = this.length
    //     if len_ < 2 : return
    //     let last = len_ - 1
    //     let first = 0
    //     while len_ > 1 {
    //         this.swap(first, last)
    //         first++
    //         last--
    //         len_ -= 2
    //     }
    // }

    ////////////////
    // Internals
    ////////////////

    fn _each(index: uint) (T, uint) !end {
        let v = this.get(index) ! throw end
        return v, index
    }

    ////////////////
    // Gc
    ////////////////

    #if @type_is_gc(T)
        fn _gc_transfer() {
            let data = this.data
            let x = this.length
            while x-- > 0 {
                mem:gc_transfer_item(@ptrv(data, T, x))
            }
        }
        fn _gc_mark() {
            let data = this.data
            let x = this.length
            while x-- > 0 {
                mem:gc_mark_item(@ptrv(data, T, x))
            }
        }
        fn _gc_mark_shared() {
            let data = this.data
            let x = this.length
            while x-- > 0 {
                mem:gc_mark_shared_item(@ptrv(data, T, x))
            }
        }
    #end

    fn _gc_free() {
        mem:free(this.data)
    }
}
