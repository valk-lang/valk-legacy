
header "sys" as sys

use mem
use io

- global coro_run_first : ?Coro (null)
- global coro_run_last : ?Coro (null)
~ global current_coro : @ignu(Coro)

- global coro_poll : ?CoroPoll (null)
- global g_coros : @ignu(Array[?Coro])
- global g_coro_indexes : @ignu(Pool[uint])
- global g_coro_poll_count : uint
- global g_coro_count : uint

struct Coro {
    args: ptr
    gc_stack: mem:StackPos
    start_func: fn(Coro)()
    handler: fn()()
    follow_up: ?Coro (null)
    next_resume: ?Coro (null)
    g_list_index: uint (0)

    s_stack: ?ptr (null)
    s_top: ptr (0 @as ptr)
    s_bottom: ptr (0 @as ptr)
    s_cont: ptr (0 @as ptr)
    s_size: uint (0)
    s_back_buf: ptr (0 @as ptr)

    test: uint (10)
    error: u32 (0)
    started: bool (false)
    done: bool (false)

    static fn new(handler: ptr, start_func: ptr) SELF {

        let gc_stack = mem:create_stack()
        let args = gc_stack.base + gc_stack.size - 128

        let coro = Coro {
            args: args
            gc_stack: gc_stack
            //
            start_func: start_func @as fn(Coro)()
            handler: handler @as fn()()
        }

        let indexes = g_coro_indexes
        if indexes.count == 0 {
            let index = g_coros.push(coro)
            coro.g_list_index = index
        } else {
            let index = indexes.get() ? 0
            g_coros.set(index, coro) _
            coro.g_list_index = index
        }

        let last = coro_run_last
        if isset(last) {
            last.next_resume = coro
            coro_run_last = coro
        } else {
            coro_run_first = coro
            coro_run_last = coro
        }

        g_coro_count++

        return coro
    }

    static fn get_poll() CoroPoll {
        let poll = coro_poll
        if poll == null {
            let p = CoroPoll.new()
            coro_poll = p
            return p
        }
        return poll @as CoroPoll
    }

    static fn yield_current() {
        let current = current_coro
        if current != null {
            await_last
        }
    }

    //////////////
    // AWAIT
    //////////////

    fn await_coro(coro: Coro) {
        let ob = coro
        let ref = ob.follow_up
        while isset(ref) {
            ob = ref
            ref = ref.follow_up
        }
        ob.follow_up = this
        this.block()
    }

    fn poll_fd(fd: FD, read: bool, write: bool) {
        let poll = Coro.get_poll()
        poll.set(fd, this, read, write)
        g_coro_poll_count++
    }

    fn await_fd(fd: FD, read: bool, write: bool) {
        this.poll_fd(fd, read, write)
        this.block()
    }

    fn await_last() {
        let last = coro_run_last
        if isset(last) {
            last.next_resume = this
            coro_run_last = this
        } else {
            coro_run_first = this
            coro_run_last = this
        }
        this.block()
    }

    //////////////
    // FLOW
    //////////////

    fn block() {
        // let here = @stack(inline [ptr, 5])
        current_coro = null
        let here = @stack(inline sys:libc_jmp_buf)
        if @setjmp(here) == 0 {
            this.s_cont = here @as ptr
            this.s_top = this.s_cont - 32

            #if OS == win
            @ptrv(here, ptr, 0) += 0x80
            #end

            // Copy stack
            let size = (this.s_bottom - this.s_top) @as uint
            let stack = this.s_stack
            let has_stack = isset(stack)
            let create_stack = !has_stack || size > this.s_size
            if create_stack {
                if has_stack : mem:free(stack @as ptr)
                stack = mem:alloc(size)
                this.s_size = size
                this.s_stack = stack
            }

            mem:copy(this.s_top, stack @as ptr, size)

            // Jump out
            @longjmp(this.s_back_buf)
        }
    }

    fn start() {
        let pad = @stack(inline [u8, 2048]) @as ptr
        this.started = true
        this.s_bottom = pad
        let start = this.start_func
        start(this)
    }
    fn continue() {
        let size = (this.s_bottom - this.s_top) @as uint
        let stack = this.s_stack @as ptr
        mem:copy(stack, this.s_top, size)
        @longjmp(this.s_cont)
    }

    static fn setjmp(buf: ptr) i32 {
        if @setjmp(buf) == 0 {
            return 0
        }
        return 1
    }
    fn resume() {
        if this.done : return
        current_coro = this

        let jmpbuf = @stack(inline sys:libc_jmp_buf)
        let current_stack = mem:stack_pos
        if @setjmp(jmpbuf) == 0 {

            #if OS == win
            @ptrv(jmpbuf, ptr, 0) += 0x80
            #end

            this.s_back_buf = jmpbuf
            mem:stack_pos = this.gc_stack
            if this.started == false {
                this.start()
            } else {
                this.continue()
            }
        }
        mem:stack_pos = current_stack

        if this.done {
            let next = this.follow_up
            if isset(next) {
                this.follow_up = null
                next.resume()
            }
        }
    }

    fn complete() {
        this.done = true
        current_coro = null
        g_coro_count--
        let index = this.g_list_index
        g_coros.set(index, null) _
        g_coro_indexes.add(index)
        @longjmp(this.s_back_buf)
    }

    //////////////
    // LOOP
    //////////////

    static fn loop() {
        let poll = Coro.get_poll()
        while g_coro_count > 0 {
            // Run coro chain
            let coro = coro_run_first
            while isset(coro) {
                coro.resume()
                let next = coro.next_resume
                coro.next_resume = null
                coro = next
            }
            coro_run_first = null
            coro_run_last = null
            // Wait for poll coros
            if g_coro_poll_count > 0 {
                let coros = poll.wait(1000) ! {
                    panic("Too many file descriptors")
                }
                g_coro_poll_count -= coros.length
                each coros as coro {
                    coro.resume()
                }
            }
            if mem:mem_usage > 10 || mem:run_gc {
                mem:gc()
            }
        }
    }

    ///////////////////
    // GC
    ///////////////////

    -pkg static fn mark_on_stack(val: u8) {
        let coros = g_coros

        each coros as coro {
            if isset(coro) {
                let adr = coro.gc_stack.base
                let top = coro.gc_stack.adr
                while adr < top {
                    let item = @ptrv(adr, ptr, 0)
                    if item != null : @ptrv(item, u8, mem:byte_on_stack_rel) = val
                    adr += sizeof(ptr)
                }
            }
        }
    }

    -pkg static fn demark_previous() {
        let coros = g_coros

        each coros as coro {
            if isset(coro) {
                let pos = coro.gc_stack
                let adr = pos.base
                let top = pos.last_adr
                pos.last_adr = pos.adr
                while adr < top {
                    let prev = @ptrv(adr, ptr, 1)
                    if prev != null {
                        let on_stack = @ptrv(prev, u8, mem:byte_on_stack_rel)
                        if on_stack == 0 {
                            let color = @ptrv(prev, u16, mem:byte_color_rel)
                            mem:colors.giveback(color)
                            mem:gc_demark(prev, color)
                        }
                        @ptrv(adr, ?ptr, 1) = null
                    }
                    adr += sizeof(ptr)
                }
            }
        }
    }

    -pkg static fn mark_stack_items() {
        let coros = g_coros
        let pc = mem:prev_colors

        each coros as coro {
            if isset(coro) {
                let adr = coro.gc_stack.base
                let top = coro.gc_stack.adr
                while adr < top {
                    let item = @ptrv(adr, ptr, 0)
                    if item != null {
                        let color = @ptrv(item, u16, mem:byte_color_rel)
                        // if color == mem:state_new {
                        if @ptrv(mem:colors.valid, u8, color) != mem:color_valid {
                            let c = mem:colors.get()
                            mem:gc_mark(item, c, true)
                            // Set previous
                            let prev = pc.get(sizeof(ptr) * 2)
                            @ptrv(prev, ptr) = item
                            @ptrv(prev, uint, 1) = c
                        }
                    }
                    adr += sizeof(ptr)
                }
            }
        }
    }

    fn _gc_mark_shared(age: u8) {
        let base = this.gc_stack.base
        let adr = this.gc_stack.adr
        while adr > base {
            adr -= sizeof(ptr)
            let item = @ptrv(adr, ptr, 0)
            if item != null : mem:gc_mark_shared(item, age)
        }
    }

    fn _gc_free() {
        this.gc_stack.free()
        let s = this.s_stack
        if isset(s) {
            mem:free(s)
        }
    }
}
