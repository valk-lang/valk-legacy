
header "sys" as sys

use mem
use os
use io

struct SocketTCP {
    ~ host: String
    ~ fd: FD
    ~ port: u16
    - addrinfo: sys:libc_addrinfo_fix

    static fn new(host: String, port: u16) SocketTCP !os_socket_create !invalid_host {

        #if OS == win
        os:WSA_init()
        #end

        #if OS == win
        let sock_fd = sys:socket(sys:AF_INET, sys:SOCK_STREAM, 0)
        let fd = io:valk_fd(sock_fd)
        #else
        let ufd = sys:socket(sys:AF_INET, sys:SOCK_STREAM, 0)
        let fd = ufd @as FD
        #end

        if fd == -1 {
            throw os_socket_create
        }

        io:fd_set_non_block(fd, true)

        // Get host info
        let addrinfo = SELF.get_addrinfo(host, port) ! {
            throw invalid_host
        }

        // Set timeout
        #if OS == win
        let timeout : i32 = 1000 * 5 // 5 seconds
        sys:setsockopt(sock_fd, sys:SOL_SOCKET, sys:SO_RCVTIMEO, @ptr_of(timeout), sizeof(i32))
        #else
        let timeout = @stack(sys:libc_timeval)
        timeout.tv_sec = 5
        timeout.tv_usec = 0
        sys:setsockopt(ufd, sys:SOL_SOCKET, sys:SO_RCVTIMEO, timeout, sizeof(inline sys:libc_timeval))
        #end

        return SocketTCP{
            host: host,
            port: port,
            fd: fd,
            addrinfo: addrinfo,
        }
    }

    fn connect() Connection !connection_failed {

        let fd = this.fd
        let ofd = io:os_fd(this.fd)

        while true {
            let err = sys:connect(ofd, this.addrinfo.ai_addr @as sys:libc_sockaddr, this.addrinfo.ai_addrlen @autocast)
            if err != 0 {
                let e = os:socket_errno()
                #if OS == win
                if e == sys:WSAEISCONN : break
                let a = e == sys:WSAEWOULDBLOCK || e == sys:WSAEINPROGRESS || e == sys:WSAEALREADY
                #else
                if e == sys:EISCONN : break
                let a = e == sys:EINPROGRESS
                #end
                if a {
                    await_fd(fd, true, true)
                    continue
                }
                throw connection_failed
            }
        }

        return Connection.new(this.fd)
    }

    fn bind() !get_addr_info !bind !listen {
        let yes : i32 = 1
        let yes_ptr = @ptr_of(yes)
        
        let err : i32 = 0

        sys:setsockopt(this.fd @autocast, sys:SOL_SOCKET, sys:SO_REUSEADDR, yes_ptr, sizeof(i32))
        err = sys:bind(this.fd @autocast, this.addrinfo.ai_addr @as sys:libc_sockaddr, this.addrinfo.ai_addrlen @autocast)

        if err != 0 {
            throw bind
        }

        err = sys:listen(this.fd @autocast, 2000000)
        if err != 0 {
            throw listen
        }
    }

    - static fn get_addrinfo(host: String, port: u16) sys:libc_addrinfo_fix !failed {

        let addrinfo : ?sys:libc_addrinfo_fix = null
        let hints = @stack(sys:libc_addrinfo_fix)
        mem:clear(hints, sizeof(inline sys:libc_addrinfo_fix))

        hints.ai_family = sys:AF_INET
        hints.ai_socktype = sys:SOCK_STREAM
        hints.ai_flags = sys:AI_PASSIVE

        let chost = host.data
        let cport = port.to_str().data
        let err = sys:getaddrinfo(chost, cport, hints, @ptr_of(addrinfo))
        if err != 0 {
            throw failed
        }
        if addrinfo == null {
            throw failed
        }

        return addrinfo @as sys:libc_addrinfo_fix
    }

    fn accept() Connection !error {

        while true {
            let ofd = sys:accept(io:os_fd(this.fd), null, null)
            let fd = io:valk_fd(ofd)

            // Error
            if fd == -1 {
                #if OS == win
                // Windows error
                let err = os:WSA_error()
                if err == sys:WSAEWOULDBLOCK {
                    await_fd(this.fd, true, false)
                    continue
                }
                #else
                // Linux / macos error
                let err = os:libc_errno()
                if err == sys:EAGAIN {
                    await_fd(this.fd, true, false)
                    continue
                }
                #end
                // On error
                throw error
            }

            return Connection.new(fd)
        }
        throw error
    }

    fn clone() SELF {
        return SELF {
            host: this.host,
            fd: this.fd,
            port: this.port,
            addrinfo: SELF.get_addrinfo(this.host, this.port) ? this.addrinfo @as sys:libc_addrinfo_fix,
        }
    }

    fn _gc_free() {
        sys:freeaddrinfo(this.addrinfo)
    }
}
